#!/bin/tcsh
#
# BUNDI - Build the Ultimate NitrOS-9 Disk Image
#
# This script builds the ULTIMATE NitrOS-9/6309 Level 2 disk image
# complete with HDB-DOS drives!
#
# The resulting image has a NitrOS-9 partition and an HDB-DOS partition.
# The first drive of the HDB-DOS partition is the HDB-DOS distribution
# diskette.  The last drive is the NitrOS-9 boot disk.
#
# Size of image is 90,624 256-byte sectors, which fits in
# $00B100 OS-9 Offset in HDB-DOS
#
# ATTENTION!  Set the HDB-DOS offset in your HDB-DOS ROM here! (must be in decimal)
set hdbdos_offset=45312		# 0x00B100
# ATTENTION!  Set the number of HDB-DOS virtual disks (must be in decimal)
set num_hdbdos_disks=128

# Check for argument
if ($2 == "") then
	echo "Usage: bundi <bootscript> <diskname>"
	exit
endif

set diskname=$2
set multiplier=2
@ os9_sectors = $hdbdos_offset * $multiplier
echo $os9_sectors
@ hdb_drives = $num_hdbdos_disks - 2

echo "########## PART  I ##########"
echo "#                           #"
echo "#    Assemble EVERYTHING!   #"
echo "#                           #"
echo "#############################"
echo
echo "Step 1 - Making the ENTIRE NitrOS-9 Project (could take a while)..."
(pushd $NITROS9DIR; make dsk; popd)>&/dev/null

echo "Step 2 - Making the HDB-DOS Product..."
(pushd $CLOUD9DIR/Products/HDB-DOS/Software; make dsk; popd)>&/dev/null

echo "Step 3 - Making the Ved Product..."
(pushd $CLOUD9DIR/Products/Ved/Software; make dsk; popd)>&/dev/null

echo "########## PART II ##########"
echo "#                           #"
echo "#   Prepare the Disk Image  #"
echo "#                           #"
echo "#############################"
echo
echo "Step 1 - Format the disk and make the boot disk"
os9 format -qe -l$os9_sectors tmp.dsk
./$1>&/dev/null
os9 gen -b=bootfile -t=boottrack tmp.dsk>&/dev/null
os9 copy -o=0 $NITROS9DIR/6309l2/modules/sysgo_dd tmp.dsk,sysgo
os9 attr tmp.dsk,sysgo -epepr
os9 dsave -e $NITROS9DIR/6309l2/latest_80d.dsk, tmp.dsk, >&/dev/null
os9 dsave -e $NITROS9DIR/3rdparty/packages/uucpbb/uucpbb21_6309.dsk, tmp.dsk, >&/dev/null
os9 dsave -e $NITROS9DIR/3rdparty/packages/cc/cc.dsk, tmp.dsk, >&/dev/null
os9 dsave -e $NITROS9DIR/3rdparty/packages/basic09/basic09v010100.dsk, tmp.dsk, >&/dev/null
os9 dsave -e $CLOUD9DIR/Products/Ved/Software/ved.dsk, tmp.dsk, >&/dev/null
os9 format -qe -ss -dd boot.dsk
os9 gen -b=bootfile -t=boottrack boot.dsk>&/dev/null
rm bootfile boottrack

echo "Step 2 - Build the HDB-DOS drives"
rm hdbdrives.dsk
decb dskini -h$hdb_drives hdbdrives.dsk
rm hdbdrives2.dsk
cat $CLOUD9DIR/Products/HDB-DOS/Software/hdbdos.dsk hdbdrives.dsk boot.dsk>hdbdrives2.dsk
rm hdbdrives.dsk boot.dsk
decb hdbconv hdbdrives2.dsk hdbdrives.dsk
rm hdbdrives2.dsk

echo "Step 3 - Put it all together"
rm $diskname
cat tmp.dsk hdbdrives.dsk>$diskname
rm tmp.dsk hdbdrives.dsk

echo "Ok, we're done! The file $diskname is now a fresh disk image."
